apply plugin: 'application'
apply plugin: 'maven'
apply plugin: 'antlr'


mainClassName = 'com.juliar.Juliar'

group = 'com.github.juliarLang'

sourceCompatibility = 1.8
targetCompatibility = 1.8

[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

repositories {
    mavenCentral()
    jcenter()
    flatDir {
        dir 'jars'
    }
}

dependencies {
    antlr 'org.antlr:antlr4:4.7'
    compile fileTree(dir: 'jars', include: '*.jar')
    compile 'org.ow2.asm:asm-all:6.0_BETA'
    compile 'com.bugsnag:bugsnag:3.+'
    testCompile 'junit:junit:4.11'

}


generateGrammarSource {
  arguments += ['-no-listener','-package','com.juliar.parser','-visitor']
}

jar {
    baseName 'juliar'
    manifest {
        attributes 'Main-Class': 'com.juliar.Juliar'
    }
    from (configurations.runtime.collect { it.isDirectory() ? it : zipTree(it) }) {
        exclude 'LICENSE'
        exclude 'module-info.class'
        exclude 'META-INF/**'
        exclude 'com/ibm/**'
    }
    exclude('com/juliar/JuliarAndroid.class')
}


task libJar(type: Jar, dependsOn: classes) {
    baseName 'juliarlib'
    manifest {
        attributes 'Main-Class': 'com.juliar.JuliarAndroid'
    }
    from(sourceSets.main.output) {
        include "**"
    }
    from (configurations.runtime.resolve().collect { it.isDirectory() ? it : zipTree(it) }) {
        exclude 'LICENSE'
        exclude 'module-info.class'
        exclude 'META-INF/**'
        exclude 'com/ibm/**'
    }
    exclude('com/juliar/Juliar.class')
    exclude('com/juliar/juliarrepl/**')
    exclude('com/nire4j/**')
    exclude('org/glassfish/json/messages.properties')
    exclude('org/antlr/v4/tool/templates/**')
    exclude('javax/annotation/**')
    exclude('com/google/**')
    exclude('com/fasterxml/**')
    exclude('com/bugsnag/**')
    exclude('org/slf4j/**')
    exclude('org/codehaus/**')
    exclude('com/juliar/web/**')
}

task webJar(type: Jar, dependsOn: classes) {
    baseName 'juliar'
    manifest {
        attributes 'Main-Class': 'com.juliar.Juliar'
    }
    from(sourceSets.main.output) {
        include "**"
    }
    from (configurations.runtime.collect { it.isDirectory() ? it : zipTree(it) }) {
        exclude 'LICENSE'
        exclude 'module-info.class'
        exclude 'META-INF/**'
        exclude 'com/ibm/**'
    }
    exclude('com/juliar/JuliarAndroid.class')
    destinationDir = file("JuliarWebCompiler/web/WEB-INF/lib/");
}


artifacts {
    archives libJar, webJar
}

test {
    include 'com/juliar/test/**'
    testLogging {
        // Show that tests are run in the command-line output
        events 'started', 'passed'
    }
}

// this step is necessary when installing both jars in the local maven repository
install {
    repositories.mavenInstaller {
        addFilter('juliarlib') { artifact, file -> artifact.name.endsWith('b') }
        pom('juliarlib').whenConfigured {
            p -> p.dependencies = p.dependencies.findAll {
                dep -> dep.artifactId != "junit" && dep.artifactId != "asm-all" && dep.artifactId != "bugsnag"
            }
        }
        pom('juliarlib').version = '0.1'
        addFilter('juliar') { artifact, file -> artifact.name.endsWith('r') }
        pom('juliar').version = '0.1'
    }
}